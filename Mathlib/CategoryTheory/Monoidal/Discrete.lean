/-
Copyright (c) 2020 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kim Morrison
-/
import Mathlib.Algebra.Group.Hom.Defs
import Mathlib.CategoryTheory.Discrete.Basic
import Mathlib.CategoryTheory.Monoidal.NaturalTransformation

/-!
# Monoids as discrete monoidal categories

The discrete category on a monoid is a monoidal category.
Multiplicative morphisms induced monoidal functors.
-/


universe u u'

open CategoryTheory Discrete MonoidalCategory

variable (M : Type u)

namespace CategoryTheory

abbrev DiscreteMonoidalCategory := MonoidalCategory (Discrete M)

instance Discrete.monoidal [Monoid M] : DiscreteMonoidalCategory M where
  tensorUnit := Discrete.mk 1
  tensorObj X Y := Discrete.mk (X.as * Y.as)
  whiskerLeft X _ _ f := eqToHom (by rw [eq_of_hom f])
  whiskerRight f X := eqToHom (by rw [eq_of_hom f])
  tensorHom f g := eqToHom (by rw [eq_of_hom f, eq_of_hom g])
  leftUnitor X := Discrete.eqToIso (one_mul X.as)
  rightUnitor X := Discrete.eqToIso (mul_one X.as)
  associator _ _ _ := Discrete.eqToIso (mul_assoc _ _ _)

instance Discrete.addMonoidal [AddMonoid M] : DiscreteMonoidalCategory M where
  tensorUnit := Discrete.mk 0
  tensorObj X Y := Discrete.mk (X.as + Y.as)
  whiskerLeft X _ _ f := eqToHom (by rw [eq_of_hom f])
  whiskerRight f X := eqToHom (by rw [eq_of_hom f])
  tensorHom f g := eqToHom (by rw [eq_of_hom f, eq_of_hom g])
  leftUnitor X := Discrete.eqToIso (zero_add X.as)
  rightUnitor X := Discrete.eqToIso (add_zero X.as)
  associator _ _ _ := Discrete.eqToIso (add_assoc _ _ _)

@[simp]
lemma Discrete.monoidal_tensorUnit_as [Monoid M] : (ùüô_ (Discrete M)).as = 1 := rfl

@[simp]
lemma Discrete.addMonoidal_tensorUnit_as [AddMonoid M] : (ùüô_ (Discrete M)).as = 0 := rfl

attribute [
  to_additive existing (attr := simps tensorObj_as leftUnitor rightUnitor associator)
  Discrete.addMonoidal
] Discrete.monoidal

section Monoid

variable [Monoid M]

variable {M} {N : Type u'} [Monoid N]

/-- A multiplicative morphism between monoids gives a monoidal functor between the corresponding
discrete monoidal categories.
-/
@[to_additive Discrete.addMonoidalFunctor]
def Discrete.monoidalFunctor (F : M ‚Üí* N) : Discrete M ‚•§ Discrete N :=
  Discrete.functor (fun X ‚Ü¶ Discrete.mk (F X))

@[to_additive (attr := simp) Discrete.addMonoidalFunctor_obj]
lemma Discrete.monoidalFunctor_obj (F : M ‚Üí* N) (m : M) :
    (Discrete.monoidalFunctor F).obj (Discrete.mk m) = Discrete.mk (F m) := rfl

instance Discrete.monoidalFunctorMonoidal (F : M ‚Üí* N) :
    (Discrete.monoidalFunctor F).Monoidal :=
    Functor.CoreMonoidal.toMonoidal
      { ŒµIso := Discrete.eqToIso F.map_one.symm
        ŒºIso := fun m‚ÇÅ m‚ÇÇ ‚Ü¶ Discrete.eqToIso (F.map_mul _ _).symm }

open Functor.LaxMonoidal Functor.OplaxMonoidal

lemma Discrete.monoidalFunctor_Œµ (F : M ‚Üí* N) :
    Œµ (monoidalFunctor F) = Discrete.eqToHom F.map_one.symm := rfl

-- @[to_additive Discrete.addMonoidalFunctor_Œ∑]
lemma Discrete.monoidalFunctor_Œ∑ (F : M ‚Üí* N) :
    Œ∑ (monoidalFunctor F) = Discrete.eqToHom F.map_one := rfl

-- @[to_additive Discrete.addMonoidalFunctor_Œº]
lemma Discrete.monoidalFunctor_Œº (F : M ‚Üí* N) (m‚ÇÅ m‚ÇÇ : Discrete M) :
    Œº (monoidalFunctor F) m‚ÇÅ m‚ÇÇ = Discrete.eqToHom (F.map_mul _ _).symm := rfl

-- @[to_additive Discrete.addMonoidalFunctor_Œ¥]
lemma Discrete.monoidalFunctor_Œ¥ (F : M ‚Üí* N) (m‚ÇÅ m‚ÇÇ : Discrete M) :
    Œ¥ (monoidalFunctor F) m‚ÇÅ m‚ÇÇ = Discrete.eqToHom (F.map_mul _ _) := rfl

-- /-- An additive morphism between add_monoids gives a
-- monoidal functor between the corresponding discrete monoidal categories. -/
-- add_decl_doc Discrete.addMonoidalFunctor

variable {K : Type u} [Monoid K]

/-- The monoidal natural isomorphism corresponding to composing two multiplicative morphisms.
-/
@[to_additive Discrete.addMonoidalFunctorComp
      /-- The monoidal natural isomorphism corresponding to
composing two additive morphisms. -/]
def Discrete.monoidalFunctorComp (F : M ‚Üí* N) (G : N ‚Üí* K) :
    Discrete.monoidalFunctor F ‚ãô Discrete.monoidalFunctor G ‚âÖ
      Discrete.monoidalFunctor (G.comp F) := Iso.refl _

--@[to_additive Discrete.addMonoidalFunctorComp_isMonoidal]
instance Discrete.monoidalFunctorComp_isMonoidal (F : M ‚Üí* N) (G : N ‚Üí* K) :
    NatTrans.IsMonoidal (Discrete.monoidalFunctorComp F G).hom where
  unit := by
    dsimp only [comp_Œµ, monoidalFunctorComp, Iso.refl, Discrete.monoidalFunctor_Œµ]
    simp [eqToHom_map]
  tensor _ _ := by
    dsimp only [comp_Œº, monoidalFunctorComp, Iso.refl, Discrete.monoidalFunctor_Œº]
    simp [eqToHom_map]

end Monoid

section AddMonoid

variable [AddMonoid M]

variable {M} {N : Type u'} [AddMonoid N]

instance Discrete.addMonoidalFunctorMonoidal (F : M ‚Üí+ N) :
    (Discrete.addMonoidalFunctor F).Monoidal :=
    Functor.CoreMonoidal.toMonoidal
      { ŒµIso := Discrete.eqToIso F.map_zero.symm
        ŒºIso := fun m‚ÇÅ m‚ÇÇ ‚Ü¶ Discrete.eqToIso (F.map_add _ _).symm }

attribute
  [to_additive existing Discrete.addMonoidalFunctorMonoidal] Discrete.monoidalFunctorMonoidal

open Functor.LaxMonoidal Functor.OplaxMonoidal

lemma Discrete.addMonoidalFunctor_Œµ (F : M ‚Üí+ N) :
    Œµ (addMonoidalFunctor F) = Discrete.eqToHom F.map_zero.symm := rfl

attribute [to_additive existing Discrete.addMonoidalFunctor_Œµ] Discrete.monoidalFunctor_Œµ

lemma Discrete.addMonoidalFunctor_Œ∑ (F : M ‚Üí+ N) :
    Œ∑ (addMonoidalFunctor F) = Discrete.eqToHom F.map_zero := rfl

attribute [to_additive existing Discrete.addMonoidalFunctor_Œ∑] Discrete.monoidalFunctor_Œ∑

lemma Discrete.addMonoidalFunctor_Œº (F : M ‚Üí+ N) (m‚ÇÅ m‚ÇÇ : Discrete M) :
    Œº (addMonoidalFunctor F) m‚ÇÅ m‚ÇÇ = Discrete.eqToHom (F.map_add _ _).symm := rfl

attribute [to_additive existing Discrete.addMonoidalFunctor_Œº] Discrete.monoidalFunctor_Œº

lemma Discrete.addMonoidalFunctor_Œ¥ (F : M ‚Üí+ N) (m‚ÇÅ m‚ÇÇ : Discrete M) :
    Œ¥ (addMonoidalFunctor F) m‚ÇÅ m‚ÇÇ = Discrete.eqToHom (F.map_add _ _) := rfl

attribute [to_additive existing Discrete.addMonoidalFunctor_Œ¥] Discrete.monoidalFunctor_Œ¥

/-- An additive morphism between add_monoids gives a
monoidal functor between the corresponding discrete monoidal categories. -/
add_decl_doc Discrete.addMonoidalFunctor

variable {K : Type u} [AddMonoid K]

instance Discrete.addMonoidalFunctorComp_isMonoidal (F : M ‚Üí+ N) (G : N ‚Üí+ K) :
    NatTrans.IsMonoidal (Discrete.addMonoidalFunctorComp F G).hom where
  unit := by
    dsimp only [comp_Œµ, addMonoidalFunctorComp, Iso.refl, Discrete.addMonoidalFunctor_Œµ]
    simp [eqToHom_map]
  tensor _ _ := by
    dsimp only [comp_Œº, addMonoidalFunctorComp, Iso.refl, Discrete.addMonoidalFunctor_Œº]
    simp [eqToHom_map]

end AddMonoid

end CategoryTheory
